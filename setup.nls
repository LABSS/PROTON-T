to-report random-religion       report one-of ["a" "b" "c" "d"] end
to-report random-birth-year     report 0 - random 80            end
to-report random-radicalization report random-float 1.0         end

to-report propensity-factors
  ; first item is weight, second item is value (between 0 and 1)
  report (list
    (list 1 [ -> random-float 1 ]) ; stochastic factor
    (list 1 [ -> ifelse-value (religion = "a") [ 1 ] [ 0 ] ])
  )
end

to-report risk-factors
  report (list
    ; weight, value (between 0 and 1)
    (list 1 [ -> propensity ])
    (list 1 [ -> 1 / ((abs (age - 18)) + 1) ])
    (list 1 [ -> radicalization ])
  )
end

to-report location-definitions
  report (list
    ; number, name, shape, color
    (list 1 "mosque"           "house bungalow"  red)
    (list 1 "school"           "house ranch"     brown)
    (list 1 "hospital"         "house colonial"  white)
    (list 1 "community center" "house two story" cyan)
    (list 2 "cafe"             "building store"  orange)
    (list 5 "workplace"        "factory"         blue)
    (list 2 "park"             "square"          (green - 2))
  )
end

to-report job-definition-list
  report (list
    ;     num-jobs  start-time duration location-type task
    (list 50        8          8        "workplace"   [ -> work ] [ [cit act] -> [ age ] of cit > 18 ])
    (list  1        8          8        "cafe"        [ -> work ] [ [cit act] -> [ age ] of cit > 18 ])
    (list  1       16          8        "cafe"        [ -> work ] [ [cit act] -> [ age ] of cit > 18 ])
  )
end

to-report mandatory-activity-definition-list
  report (list
    ; start-time duration location-type task criteria
    (list 0 8 "residence" [ -> sleep ] [ [cit act] -> [ residence ] of cit = [ location-here ] of act ] )
    (list 8 8 "school"    [ -> study ] [ [cit act] -> [ age >= 5 and age <= 18 ] of cit ])
  )
end

to-report free-time-activity-definition-list
  report (list
    (list "residence" [ -> socialize ] [ [cit act] -> [ residence ] of cit = [ location-here ] of act ])
    (list "cafe"      [ -> socialize ] [ -> true ])
    (list "park"      [ -> socialize ] [ -> true ])
  )
end

to work ; generic work activity
end

to socialize
end